def ggRepo = System.getenv('GG_REPO')

allprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'maven'
  apply plugin: 'idea'

  version = '0.0.1'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  group = "kz.diploma.prosecurity"

  buildscript {
    repositories {
      mavenLocal()
      mavenCentral()
    }
  }

  repositories {
    mavenLocal()
    if (ggRepo?.trim()) maven { url ggRepo.trim() }
    mavenCentral()
  }
}

subprojects {

  dependencies {
    ext.jacksonVersion = "2.7.3"
    ext.depinjectVersion = '2.1.3'
    ext.greetgoUtilVersion = '2.0.0'
    ext.greetgoDbVersion = '2.3.1'
    ext.greetgoConfVersion = '1.0.2'
    ext.greetgoTsVersion = '0.0.5'
    ext.greetgoMvcVersion = "3.0.0"
    ext.greetgoSecurityVersion = "0.0.1"
    ext.jettyVersion = '9.4.0.v20161208'
    ext.guavaVersion = '23.0'
    ext.postgresJdbcVersion = '9.4-1202-jdbc42'
    ext.firebaseAdminVersion = "6.8.0"

    testCompile 'org.testng:testng:6.5.1'
    compile 'org.easytesting:fest-assert-core:2.0M10'
  }

  tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
  }

  test.useTestNG()

  idea.module.inheritOutputDirs = false
  idea.module.outputDir = file("$buildDir/classes/main/")
  idea.module.testOutputDir = file("$buildDir/classes/test/")

  [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
  [compileJava, compileTestJava]*.options*.debugOptions*.debugLevel = "source,lines,vars"

  sourceSets.main.java.srcDirs = ["src"]
  sourceSets.test.java.srcDirs = ["test_src"]
  sourceSets.main.resources.srcDirs = ["src_resources"]
  sourceSets.test.resources.srcDirs = ["test_resources"]

  sourceSets.main.resources {
    srcDirs += sourceSets.main.java.srcDirs
    exclude '**/*.java'
  }
  sourceSets.test.resources {
    srcDirs += sourceSets.test.java.srcDirs
    exclude '**/*.java'
  }

  jar {
    manifest {
      attributes 'Implementation-Title': project.name
      attributes 'Implementation-Version': version
      attributes 'Implementation-Vendor': "greetgo!"
      attributes 'Created-By': System.getProperty("user.name")
      attributes 'Created-At': new Date().format("yyyy-MM-dd/HH:mm:ss")
    }
  }
}
